função converteStrHexEmStrDec(s : string): string
var
  função confereStrHex(i : inteiro): lógico
  início
    se i = 1 então
      se (s[i] = '-' e tamanho(s) > 1) ou (s[i] >= '0' e s[i] <= '9') ou (s[i] >= 'A' e s[i] <= 'F') então
        retorne VERDADEIRO
      senão
        retorne FALSO
      fimse
    senão    
      se não ((s[i] >= '0' e s[i] <= '9') ou (s[i] >= 'A' e s[i] <= 'F')) então
        retorne FALSO
      senão
        retorne confereStrHex(i - 1)
      fimse
    fimse
  fimfunção

  função converteStrHexEmDec(n : inteiro, fator): inteiro
  início
    se n = 0 ou (n = 1 e s[1] = '-') então
      retorne 0
    senão
      se s[n] >= 'A' então
        retorne (asc(s[n]) - asc('A') + 10) * fator + converteStrHexEmDec(n - 1, fator * 16) 
      senão
        retorne (asc(s[n]) - asc('0')) * fator + converteStrHexEmDec(n - 1, fator * 16)
      fimse
    fimse
  fimfunção   
      
  função converteDecEmStr(dec : inteiro): string
  início
    se dec = 0 então
      retorne ""
    senão
      retorne converteDecEmStr(dec div 10) + chr(dec mod 10 + asc('0'))
    fimse
  fimfunção  

início
  se tamanho(s) = 0 então
    retorne ""
  fimse

  se s = "0" ou s = "-0" então
    retorne "0"
  fimse

  se confereStrHex(tamanho(s)) então
    se s[1] = '-' então
      retorne '-' + converteDecEmStr(converteStrHexEmDec(tamanho(s), 1))
    senão
      retorne converteDecEmStr(converteStrHexEmDec(tamanho(s), 1))
    fimse
  senão
    retorne ""
  fimse  
fimfunção
