tipos
    PNo = ponteiro para TNo
    TNo = registro
            info : string
            prox : PNo
          fimregistro

função mesclaListas(l1, l2 : PNo): PNo
var
    head, temp1, temp2, ant : PNo
início
    se l1 = NULO então
        retorne l2
    fimse
    se l2 = NULO então
        retorne l1
    fimse

    temp1 <- l1
    temp2 <- l2

    se temp1^.info <= temp2^.info então
        head <- temp1
        temp1 <- temp1^.prox
    senão
        head <- temp2
        temp2 <- temp2^.prox
    fimse

    ant <- head
    enquanto temp1 != NULO e temp2 != NULO faça
        se temp1^.info <= temp2^.info então
            ant^.prox <- temp1
            temp1 <- temp1^.prox
        senão
            ant^.prox <- temp2
            temp2 <- temp2^.prox
        fimse
        ant <- ant^.prox
    fimenquanto

    se temp1 != NULO então
        ant^.prox <- temp1
    senão
        ant^.prox <- temp2
    fimse

    retorne head
fimfunção
