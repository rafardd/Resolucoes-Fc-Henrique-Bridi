tipos 
  PNo : ponteiro para TNo
  TNo : registro
          info : string
          prox : PNo
        fimregistro

função removeEmPosição(ref l: PNo; p: inteiro): lógico
var
  temp, ant : PNo
início
  se l = NULO ou p <= 0 então 
    retorne FALSO
  fimse

  temp <- l
  se p = 1 então
    l <- l^.prox
    desaloca(temp)
    retorne VERDADEIRO
  fimse

  ant <- NULO
  enquanto temp != NULO e p > 1 faça
    ant <- temp
    temp <- temp^.prox
    p <- p - 1
  fimenquanto

  se temp = NULO então
    retorne FALSO
  senão
    ant^.prox <- temp^.prox
    desaloca(temp)
    retorne VERDADEIRO
  fimse
fimfunção