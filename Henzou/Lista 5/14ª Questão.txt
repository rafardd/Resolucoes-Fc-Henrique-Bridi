função mediana(s: string): inteiro
var
  frequência : vetor[0..3000] de inteiro
  i, numAtual, numAnterior, quantidade : inteiro
  f : sequência de inteiro
início
  quantidade <- 0

  para i de 0 até 3000 faça
    frequência[i] <- 0
  fimpara
  se abre(f, s) então
    enquanto não fds(f) faça
      leia(f, numAtual)
      frequência[numAtual] <- frequência[numAtual] + 1
      quantidade <- quantidade + 1
    fimenquanto
    fecha(f)
  senão
    pare
  fimse

  se quantidade = 0 então
    pare
  fimse
  
  i <- 1
  numAtual <- 0
  numAnterior <- 0

  enquanto i <= quantidade div 2 + 1 faça
    se frequência[numAtual] = 0 então
      numAtual <- numAtual + 1
    senão
      frequência[numAtual] <- frequência[numAtual] - 1      
      se i = quantidade div 2 então
        numAnterior <- numAtual
      fimse
      i <- i + 1
    fimse
  fimenquanto
  
  se quantidade mod 2 = 0 então
    retorne (numAnterior + numAtual) / 2
  senão
    retorne numAtual
  fimse
fimfunção
    
