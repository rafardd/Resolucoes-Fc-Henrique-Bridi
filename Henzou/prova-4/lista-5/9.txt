procedimento mesclaSequências(s1, s2, s3)
var
  n1, n2 : real
  f1, f2, f3 : sequência de real
  fds_sequência1, fds_sequência2 : lógico

  função verificaFds(ref f: sequência de real; ref num: inteiro): lógico
  início
    se não fds(f) então
      leia(f, num)
      retorne FALSO
    senão
      retorne VERDADEIRO
    fimse
  fimfunção

início
  se abre(f1, s1) então
    se abre(f2, s2) então
      se abre(f3, s3) então
        fds_sequência1 <- verificaFds(f1, n1)
        fds_sequência2 <- verificaFds(f2, n2)
        enquanto não fds_sequência1 ou não fds_sequência2 faça
          se fds_sequência1 ou (não fds_sequência2 e n1 > n2) então
            escreva(f3, n2)
            fds_sequência2 <- verificaFds(f2, n2)
          senão
            escreva(f3, n1)
            fds_sequência1 <- verificaFds(f1, n1)
          fimse
        fimenquanto
        fecha(f3)
        fecha(f2)
        fecha(f1)
      senão 
        fecha(f2)
        fecha(f1)
      fimse
    senão
      fecha(f1)
    fimse
  fimse
fimprocedimento
