tipos
    PNo : ponteiro para TNo
    TNo : registro
            info : inteiro
            prox : PNo
          fimregistro

procedimento separaParesEÍmpares(l1, l2 : PNo; ref lPar, lÍmpar : PNo)
var
    ultPar, ultÍmpar : PNo

    procedimento separaNós(temp1, temp2 : PNo; ref lPar, lÍmpar, ultPar, ultÍmpar : PNo)
    var
        atual : PNo
    início
        se temp1 = NULO e temp2 = NULO então
            pare
        fimse

        se temp2 = NULO ou (temp1 != NULO e temp1^.info <= temp2^.info) então
            atual <- temp1
            temp1 <- temp1^.prox
        senão
            atual <- temp2
            temp2 <- temp2^.prox
        fimse

        atual^.prox <- NULO

        se atual^.info mod 2 = 0 então
            se lPar = NULO então
                lPar <- atual
                ultPar <- atual
            senão
                ultPar^.prox <- atual
                ultPar <- atual
            fimse
        senão
            se lÍmpar = NULO então
                lÍmpar <- atual
                ultÍmpar <- atual
            senão
                ultÍmpar^.prox <- atual
                ultÍmpar <- atual
            fimse
        fimse
        separaNós(temp1, temp2, lPar, lÍmpar, ultPar, ultÍmpar)
    fimprocedimento

início
    lPar <- NULO
    lÍmpar <- NULO
    ultPar <- NULO
    ultÍmpar <- NULO

    separaNós(l1, l2, lPar, lÍmpar, ultPar, ultÍmpar)
fimprocedimento
