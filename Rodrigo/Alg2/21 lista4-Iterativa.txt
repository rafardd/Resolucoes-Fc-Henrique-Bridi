tipo 
    PNo : ponteiro para TNo
    TNo : registro
            info : strings
            prox : PNo
        fimregistro 

funcao removeNoPosicaoP(ref head : PNo, p : inteiro) : logico
var
    aux, ant : PNo
    i : inteiro
inicio
    se head = NULO ou p <= 0 entao
        retorne falso
    fimse
    
    se p = 1 entao
        aux <- head
        head <- aux^.prox
        desaloca(aux)
        retorne verdadeiro
    fimse

    i <- 2
    ant <- head
    aux <- head^.prox
    enquanto aux != NULO e i < p faca
        ant <- aux
        aux <- aux^.prox
        i <- i + 1
    fimenquanto

    se aux = NULO entao
        retorne falso
    fimse

    ant^.prox <- aux^.prox
    desaloca(aux)
    retorne verdadeiro
fimfuncao

