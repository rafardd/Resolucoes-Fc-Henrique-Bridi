tipo 
    PNo : ponteiro para TNo
    TNo : registro
            info : inteiro
            prox : PNo
        fimregistro

funcao insereXNaListaOrdenada(ref head : PNo; x : inteiro) : logico
var
    aux, novo, ant : PNo
inicio
    novo <- aloca(TNo)
    se novo = NULO entao
        retorne falso 
    fimse
    se head = NULO ou head^.info >= x entao
        novo^.prox <- head // Se head = NULO, da tudo certo
        novo^.info <- x 
        head <- novo
        retorne verdadeiro
   fimse
   aux <- head
   enquanto aux != NULO e x > aux^.info faca
        ant <- aux
        aux <- aux^.prox
    fimenquanto
    novo^.prox <- aux // Já que aux é igual a ant^.prox
    ant^.prox <- novo
    novo^.info <- x 
    retorne verdadeiro
fimfuncao
    