TIPOS
    PNo = PONTEIRO PARA TNo
    TNo =
        REGISTRO
            coef : real
            exp : real
            prox : PNo
        FIM REGISTRO

FUNCAO soma_polinomio(p1,p2: PNo):PNo
VAR
    coef : real
    p, ult, novo, aux: PNo

    FUNCAO somou(pp: PNo ; c: real):lógico
    INICIO
        novo <- aloca(TNo)
        SE novo = NULO ENTAO    // DEU ERRADO. SEM MEMÓRIA. DESTROI TUDO.
            aux <- p
            ENQUANTO p != NULO FAÇA
                p <- p^.prox
                desaloca(aux)
                aux <- p
            FIM ENQUANTO
            ult <- NULO
            RETORNE falso
        FIMSE
        novo^.coef <- c
        novo^.prox <- NULO
        novo^.exp <- pp^.exp
        SE p = NULO ENTAO
            p <- novo
        SENAO
            ult^.prox <- novo
        FIMSE
        ult <- novo
        RETORNE verdadeiro
    FIM FUNCAO

INICIO
    ult <- NULO
    p <- NULO
    ENQUANTO p1 != NULO E p2 != NULO FAÇA
        SE p1^.exp = p2^.exp ENTAO
            coef <- p1^.coef + p2^.coef
            SE coef != 0 ENTAO
                SE NÃO somou(p1, coef) ENTAO
                    RETORNE NULO
                FIM SE
            FIMSE
            p1 <- p1^.prox
            p2 <- p2^.prox
        SENAO
            SE p1^.exp > p2^.exp ENTAO
                SE NÃO somou(p1, p1^.coef) ENTAO
                    RETORNE NULO
                FIM SE
                p1 <- p1^.prox
            SENAO
                SE NÃO somou(p2, p2^.coef) ENTAO
                    RETORNE NULO
                FIM SE
                p2 <- p2^.prox
            FIMSE
        FIMSE
    FIM ENQUANTO
    ENQUANTO p1 != NULO FAÇA
        SE NÃO somou(p1, p1^.coef) ENTAO
            RETORNE NULO
        FIM SE
        p1 <- p1^.prox
    FIM ENQUANTO
    ENQUANTO p2 != NULO FAÇA
        SE NÃO somou(p2, p2^.coef) ENTAO
            RETORNE NULO
        FIM SE
        p2 <- p2^.prox
    FIM ENQUANTO
    RETORNE p
FIM FUNCAO